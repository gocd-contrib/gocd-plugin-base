/*
 * Copyright 2019 Thoughtworks, Inc.
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 *     http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */

plugins {
  id 'java-library'
  id 'maven-publish'
  id 'signing'
  id 'io.github.gradle-nexus.publish-plugin' version '1.3.0'
}

apply from: "https://raw.githubusercontent.com/gocd/gocd-plugin-gradle-task-helpers/master/test-task-helper.gradle?_=${(int) (new Date().toInstant().epochSecond / 60)}"

group = "cd.go.plugin.base"
version = '0.0.6'
description = 'Helper utility to write gocd plugins'

repositories {
  mavenCentral()
}

java {
  sourceCompatibility = JavaVersion.VERSION_11
  targetCompatibility = JavaVersion.VERSION_11
}

ext {
  lombokVersion = '1.18.30'
  gocdPluginApiVersion = '23.4.0'
  gsonVersion = '2.10.1'
}

dependencies {
  api "com.google.code.gson:gson:$gsonVersion"

  compileOnly "org.projectlombok:lombok:$lombokVersion"
  annotationProcessor "org.projectlombok:lombok:$lombokVersion"

  compileOnly "cd.go.plugin:go-plugin-api:$gocdPluginApiVersion"

  implementation platform('org.junit:junit-bom:5.10.1')
  compileOnly 'org.junit.jupiter:junit-jupiter-api'
  compileOnly 'org.junit.jupiter:junit-jupiter-params'
  testImplementation 'org.junit.jupiter:junit-jupiter-api'
  testImplementation 'org.junit.jupiter:junit-jupiter-params'
  testRuntimeOnly 'org.junit.jupiter:junit-jupiter-engine'
  testRuntimeOnly 'org.junit.platform:junit-platform-launcher'
  testImplementation 'org.assertj:assertj-core:3.24.2'
  testImplementation 'org.mockito:mockito-core:5.7.0'
  testImplementation "cd.go.plugin:go-plugin-api:$gocdPluginApiVersion"
  testImplementation 'org.skyscreamer:jsonassert:1.5.1'
}

test {
  useJUnitPlatform()
}

sourceSets {
  main.compileClasspath += project.configurations.compileClasspath

  examples {
    compileClasspath += sourceSets.main.output
    compileClasspath += sourceSets.main.compileClasspath
  }
}

task javadocJar(type: Jar) {
  archiveClassifier = 'javadoc'
  from javadoc
}

task sourcesJar(type: Jar) {
  archiveClassifier = 'sources'
  from sourceSets.main.allSource
}

artifacts {
  archives jar
  archives javadocJar
  archives sourcesJar
}

publishing {
  publications {
    mavenJava(MavenPublication) {
      from components.java
      artifact sourcesJar
      artifact javadocJar

      pom {
        name = project.name
        description = project.description
        url = 'https://github.com/gocd-contrib/gocd-plugin-base'
        inceptionYear = '2018'

        licenses {
          license {
            name = 'Apache License, Version 2.0'
            url = 'https://www.apache.org/licenses/LICENSE-2.0'
            distribution = 'repo'
          }
        }
        developers {
          developer {
            id = 'GoCD Committers'
            email = 'go-cd-dev@googlegroups.com'
            url = 'https://www.gocd.org'
          }
        }
        scm {
          url = 'https://github.com/gocd-contrib/gocd-plugin-base'
          connection = 'scm:https://github.com/gocd-contrib/gocd-plugin-base'
          developerConnection = 'https://github.com/gocd-contrib/gocd-plugin-base'
        }

        organization {
          name = 'Thoughtworks, Inc.'
          url = 'https://www.thoughtworks.com'
        }

        issueManagement {
          url = 'https://github.com/gocd-contrib/gocd-plugin-base/issues'
          system = 'GitHub'
        }
      }
    }
  }
}

signing {
  sign publishing.publications.mavenJava
}

project.ext.'signing.keyId' = System.getenv('GOCD_GPG_KEY_ID')
project.ext.'signing.password' = System.getenv('GOCD_GPG_PASSPHRASE')
project.ext.'signing.secretKeyRingFile' = System.getenv('GOCD_GPG_KEYRING_FILE')

nexusPublishing {
  packageGroup = 'cd.go'
  repositories {
    sonatype {
      username = System.getenv('GOCD_NEXUS_USERNAME')
      password = System.getenv('GOCD_NEXUS_PASSWORD')
    }
  }
}

javadoc {
  title = 'Helper utility to write gocd plugins'

  options {
    windowTitle = 'Helper utility to write gocd plugins'
    docTitle = 'Helper utility to write gocd plugins'
    header = 'Helper utility to write gocd plugins'
    bottom = "Copyright &copy; ${Calendar.getInstance().get(Calendar.YEAR).toString()} Thoughtworks, Inc."
  }

  if (JavaVersion.current().isJava9Compatible()) {
    options.addBooleanOption('html5', true)
  }
}
