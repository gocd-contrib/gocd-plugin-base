/*
 * Copyright 2019 ThoughtWorks, Inc.
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 *     http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */

plugins {
  id 'java'
  id 'maven-publish'
  id 'signing'
  id 'io.github.gradle-nexus.publish-plugin' version '1.1.0'
}

apply from: "https://raw.githubusercontent.com/gocd/gocd-plugin-gradle-task-helpers/master/test-task-helper.gradle?_=${(int) (new Date().toInstant().epochSecond / 60)}"

group = "cd.go.plugin.base"
version = '0.0.3'
description = 'Helper utility to write gocd plugins'

repositories {
  mavenCentral()
}

java {
  sourceCompatibility = JavaVersion.VERSION_11
  targetCompatibility = JavaVersion.VERSION_11
}

dependencies {
  implementation group: 'com.google.code.gson', name: 'gson', version: '2.9.0'
  implementation group: 'org.apache.commons', name: 'commons-lang3', version: '3.12.0'

  compileOnly group: 'org.projectlombok', name: 'lombok', version: '1.18.24'
  annotationProcessor group: 'org.projectlombok', name: 'lombok', version: '1.18.22'

  compileOnly group: 'cd.go.plugin', name: 'go-plugin-api', version: '22.1.0'
  compileOnly group: 'org.junit.jupiter', name: 'junit-jupiter-api', version: '5.8.2'
  compileOnly group: 'org.junit.jupiter', name: 'junit-jupiter-params', version: '5.8.2'

  testImplementation group: 'org.junit.jupiter', name: 'junit-jupiter-api', version: '5.8.2'
  testImplementation group: 'org.junit.jupiter', name: 'junit-jupiter-params', version: '5.8.2'
  testRuntimeOnly group: 'org.junit.jupiter', name: 'junit-jupiter-engine', version: '5.8.2'
  testImplementation group: 'org.assertj', name: 'assertj-core', version: '3.23.1'
  testImplementation group: 'org.mockito', name: 'mockito-core', version: '4.6.1'
  testImplementation group: 'cd.go.plugin', name: 'go-plugin-api', version: '21.4.0'
  testImplementation group: 'org.skyscreamer', name: 'jsonassert', version: '1.5.1'
}

test {
  useJUnitPlatform()
}

sourceSets {
  main.compileClasspath += project.configurations.compileClasspath

  examples {
    compileClasspath += sourceSets.main.output
    compileClasspath += sourceSets.main.compileClasspath
  }
}

task javadocJar(type: Jar) {
  archiveClassifier = 'javadoc'
  from javadoc
}

task sourcesJar(type: Jar) {
  archiveClassifier = 'sources'
  from sourceSets.main.allSource
}

artifacts {
  archives jar
  archives javadocJar
  archives sourcesJar
}

publishing {
  publications {
    mavenJava(MavenPublication) {
      from components.java
      artifact sourcesJar
      artifact javadocJar

      pom {
        name = project.name
        description = project.description
        url = 'https://github.com/gocd-contrib/gocd-plugin-base'
        inceptionYear = '2018'

        licenses {
          license {
            name = 'Apache License, Version 2.0'
            url = 'https://www.apache.org/licenses/LICENSE-2.0'
            distribution = 'repo'
          }
        }
        developers {
          developer {
            id = 'GoCD Committers'
            email = 'go-cd-dev@googlegroups.com'
            url = 'https://www.gocd.org'
          }
        }
        scm {
          url = 'https://github.com/gocd-contrib/gocd-plugin-base'
          connection = 'scm:https://github.com/gocd-contrib/gocd-plugin-base'
          developerConnection = 'https://github.com/gocd-contrib/gocd-plugin-base'
        }

        organization {
          name = 'ThoughtWorks, Inc.'
          url = 'https://www.thoughtworks.com'
        }

        issueManagement {
          url = 'https://github.com/gocd-contrib/gocd-plugin-base/issues'
          system = 'GitHub'
        }
      }
    }
  }
}

signing {
  sign publishing.publications.mavenJava
}

project.ext.'signing.keyId' = System.getenv('GOCD_GPG_KEY_ID')
project.ext.'signing.password' = System.getenv('GOCD_GPG_PASSPHRASE')
project.ext.'signing.secretKeyRingFile' = System.getenv('GOCD_GPG_KEYRING_FILE')

nexusPublishing {
  packageGroup = 'cd.go'
  repositories {
    sonatype {
      username = System.getenv('GOCD_NEXUS_USERNAME')
      password = System.getenv('GOCD_NEXUS_PASSWORD')
    }
  }
}

javadoc {
  title = 'Helper utility to write gocd plugins'

  options {
    windowTitle = 'Helper utility to write gocd plugins'
    docTitle = 'Helper utility to write gocd plugins'
    header = 'Helper utility to write gocd plugins'
    bottom = "Copyright &copy; ${Calendar.getInstance().get(Calendar.YEAR).toString()} ThoughtWorks, Inc."
  }

  if (JavaVersion.current().isJava9Compatible()) {
    options.addBooleanOption('html5', true)
  }
}
